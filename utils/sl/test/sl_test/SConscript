# Copyright (c) 2016-2018 Intel Corporation
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
"""Test various build combinations involving prerequisites"""

Import('PREREQS ENV')

def run_test(env, *tests):
    """Build a single test involving one or more prerequisites"""
    cloned_env = env.Clone()
    my_tests = []
    for test in tests:
        my_tests.append(test)
        cloned_env.Append(CPPDEFINES=[test.upper()])
        if not PREREQS.check_component(test):
            print("Check for component failed")
            raise Exception("Component %s not found" % test)
        #Try the same test again to ensure check_component works
        #when called a second time (IOF-768)
        if not PREREQS.check_component(test):
            print("Check for component failed")
            raise Exception("Component %s not found" % test)
    PREREQS.require(cloned_env, *tests)
    target_name = "_".join(my_tests)
    prog = cloned_env.Program(target=target_name,
                              source=Glob('test.c'),
                              OBJPREFIX=target_name)

    return prog

def run_check(env, tname, source, expected=None, headers_only=False):
    """Check compilation for expected links"""
    cloned_env = env.Clone()

    if expected is None:
        expected = []

    cloned_env.Replace(LIBS=[])
    if headers_only:
        PREREQS.require(cloned_env, 'sl_project2', headers_only=True)
    else:
        PREREQS.require(cloned_env, 'sl_project2', sl_project2_libs=expected)
    proglist = ['gen_script.sh']
    proglist += cloned_env.Program(tname, source, OBJSUFFIX=tname)
    prog = cloned_env.Command("%s.sh" % tname, proglist,
                              ["$SOURCES %s" % " ".join(expected)])

    return prog


PROGS = []
PROGS += run_test(ENV, 'sl_project1')
PROGS += run_test(ENV, 'sl_project1', 'sl_project2')
PROGS += run_test(ENV, 'sl_project3')
PROGS += run_test(ENV, 'sl_project4')
PROGS += run_test(ENV, 'sl_project4', 'sl_project3')
PROGS += run_test(ENV, 'openpa')
PROGS += run_test(ENV, 'sl_project4', 'openpa2')
PROGS += run_check(ENV, 'pthread_test', 'pthread_test.c', ['pthread'])
PROGS += run_check(ENV, 'simple1', 'simple.c')
PROGS += run_check(ENV, 'simple2', 'simple.c', headers_only=True)

TESTS = ENV.RunTests(PROGS)
AlwaysBuild(TESTS)
