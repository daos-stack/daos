"""Build common libraries"""
import daos_build

COMMON_FILES = ['debug.c', 'mem.c', 'fail_loc.c', 'lru.c',
                'misc.c', 'pool_map.c', 'sort.c', 'btree.c', 'prop.c',
                'btree_class.c', 'tse.c', 'rsvc.c', 'checksum.c',
                'drpc.c', 'drpc.pb-c.c', 'proc.c',
                'acl_api.c', 'acl_util.c', 'acl_principal.c', 'cont_props.c',
                'dedup.c', 'profile.c', 'compression.c', 'compression_isal.c',
                'compression_qat.c', 'multihash.c', 'multihash_isal.c',
                'cipher.c', 'cipher_isal.c', 'qat.c']


def build_daos_common(env, client, prereqs):
    """ Building non-pmem version for client's common lib"""
    denv = env.Clone()

    if client:
        libname = 'libdaos_common'
    else:
        denv.Append(CPPDEFINES=['-DDAOS_PMEM_BUILD'])
        denv.Append(OBJPREFIX="v_")
        libname = 'libdaos_common_pmem'
        prereqs.require(denv, 'pmdk')

    common = daos_build.library(denv, libname, COMMON_FILES)
    denv.Install('$PREFIX/lib64/', common)

def build_dts_library(env):
    """Build libdts library"""
    Import('prereqs')

    denv = env.Clone()

    prereqs.require(denv, 'argobots', 'protobufc')

    if not GetOption('help') and not GetOption('clean'):
        mpi = daos_build.configure_mpi(denv, [])
        if mpi is None:
            print("\nSkipping compilation for tests that need MPI")
            print("Install and load mpich or openmpi\n")
            return

    libraries = ['cmocka', 'daos_tests', 'vos', 'bio', 'abt']

    # Add runtime paths for daos libraries
    denv.AppendUnique(LINKFLAGS=[r'-Wl,-rpath=\$$ORIGIN/daos_srv'])

    denv.AppendUnique(LIBPATH=["../vos"])
    denv.AppendUnique(LIBPATH=["../bio"])

    denv.AppendUnique(CPPPATH=["../tests/suite"])

    dts_lib = daos_build.library(denv, 'libdts', ['dts.c'], LIBS=libraries)
    denv.Install('$PREFIX/lib64/', dts_lib)

def scons():
    """Execute build"""
    Import('env', 'prereqs')

    env.AppendUnique(LIBPATH=[Dir('.')])

    # Hack alert, the argobots headers are required but the shared
    # library isn't so add the dependency so the include path
    # is correct, but set _libs to [] so that the library isn't
    # linked in.

    # Arm can build ISA-L
    prereqs.require(env, 'isal')
    prereqs.require(env, 'isal_crypto')

    denv = env.Clone(LIBS=[])
    prereqs.require(denv, 'isal', 'isal_crypto', 'protobufc')
    denv.AppendUnique(LIBS=['cart', 'gurt', 'lz4'])
    build_daos_common(denv, True, prereqs)
    build_daos_common(denv, False, prereqs)


    prereqs.require(env, 'pmdk')
    control_tgts = [File('control.c')]
    Export('control_tgts')

    tlib_env = denv.Clone()
    tlib_env.AppendUnique(LIBS=['json-c'])

    prereqs.require(tlib_env, 'argobots')

    tests_lib_src = ['tests_lib.c', 'tests_dmg_helpers.c']
    tests_lib = daos_build.library(tlib_env, 'libdaos_tests', tests_lib_src)
    tlib_env.Install('$PREFIX/lib64/', tests_lib)

    tenv = tlib_env.Clone()
    prereqs.require(tenv, 'pmdk')
    SConscript('tests/SConscript', exports='tenv')

    build_dts_library(denv)

if __name__ == "SCons.Script":
    scons()
