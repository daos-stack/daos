"""Build common libraries"""
import daos_build

def scons():
    """Execute build"""
    Import('env', 'prereqs', 'platform_arm')

    env.AppendUnique(LIBPATH=[Dir('.')])

    # Hack alert.   Add pmem to pmdk libs so libpmem is found
    # in the rpath.   It is normally only included by libpmemobj
    # but that library doesn't contain the installed location
    # in the rpath.
    prereqs.require(env, 'pmdk', 'argobots', 'cart', 'readline',
                    'protobufc', pmdk_libs=['pmemobj', 'pmem'])

    if platform_arm:
        prereqs.require(env, 'mchecksum')
    else:
        prereqs.require(env, 'isal')

    denv = env.Clone()
    common_src = ['debug.c', 'mem.c', 'fail_loc.c', 'lru.c',
                  'misc.c', 'pool_map.c', 'sort.c', 'btree.c',
                  'btree_class.c', 'tse.c', 'rsvc.c', 'checksum.c',
                  'drpc.c', 'drpc.pb-c.c', 'proc.c', 'acl_api.c']

    common = daos_build.library(denv, 'libdaos_common', common_src)
    denv.Install('$PREFIX/lib/', common)

    tests_lib_src = ['tests_lib.c']
    tests_lib = daos_build.library(denv, 'libdaos_tests', tests_lib_src)
    denv.Install('$PREFIX/lib/', tests_lib)

    SConscript('tests/SConscript', exports='denv')

if __name__ == "SCons.Script":
    scons()
