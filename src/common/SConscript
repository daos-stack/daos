"""Build common libraries"""
import daos_build

def scons():
    """Execute build"""
    Import('env', 'prereqs', 'platform_arm')

    env.AppendUnique(LIBPATH=[Dir('.')])

    # Hack alert, the argobots headers are required but the shared
    # library isn't so add the dependency so the include path
    # is correct, but set _libs to [] so that the library isn't
    # linked in.

    prereqs.require(env, 'pmdk')

    if not platform_arm:
        prereqs.require(env, 'isal')

    denv = env.Clone()

    prereqs.require(denv, 'protobufc')
    denv.AppendUnique(LIBS=['cart', 'gurt'])
    common_src = ['debug.c', 'mem.c', 'fail_loc.c', 'lru.c',
                  'misc.c', 'pool_map.c', 'sort.c', 'btree.c', 'prop.c',
                  'btree_class.c', 'tse.c', 'rsvc.c', 'checksum.c',
                  'drpc.c', 'drpc.pb-c.c', 'proc.c',
                  'acl_api.c', 'acl_util.c', 'acl_principal.c',
                  'profile.c']

    common = daos_build.library(denv, 'libdaos_common', common_src)
    denv.Install('$PREFIX/lib64/', common)

    tenv = denv.Clone()

    prereqs.require(tenv, 'argobots')

    tests_lib_src = ['tests_lib.c']
    tests_lib = daos_build.library(tenv, 'libdaos_tests', tests_lib_src)
    tenv.Install('$PREFIX/lib64/', tests_lib)

    SConscript('tests/SConscript', exports='tenv')

if __name__ == "SCons.Script":
    scons()
