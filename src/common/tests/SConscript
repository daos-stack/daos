"""Build doas common tests"""
import daos_build

def scons():
    """Execute build"""
    Import('denv')

    common_test_utils = denv.SharedObject(['test_mocks.c', 'test_utils.c'])
    Export('common_test_utils')

    utest_utils = denv.SharedObject('utest_common.c')
    Export('utest_utils')

    daos_build.test(denv, 'btree', ['btree.c', utest_utils],
                    LIBS=['daos_common', 'gurt', 'pmemobj', 'cmocka'])
    daos_build.test(denv, 'umem_test', ['umem_test.c', utest_utils],
                    LIBS=['daos_common', 'gurt', 'pmemobj', 'cmocka'])
    daos_build.test(denv, 'btree_direct', ['btree_direct.c', utest_utils],
                    LIBS=['daos_common', 'gurt', 'pmemobj', 'cmocka'])
    daos_build.test(denv, 'other', 'other.c',
                    LIBS=['daos_common', 'gurt', 'cart'])
    daos_build.test(denv, 'checksum', 'checksum.c',
                    LIBS=['daos_common', 'gurt', 'cart'])
    daos_build.test(denv, 'lru', 'lru.c',
                    LIBS=['daos_common', 'gurt', 'cart'])
    daos_build.test(denv, 'sched', 'sched.c',
                    LIBS=['daos_common', 'gurt', 'cart', 'cmocka'])
    daos_build.test(denv, 'abt_perf', 'abt_perf.c',
                    LIBS=['daos_common', 'gurt', 'abt'])

    unit_env = denv.Clone()
    # Some syscalls are aliased when fortification is enabled, which makes
    # mocking in unit tests into a headache.
    # The compiler decides this on its own, but let's force the issue.
    unit_env.Append(CCFLAGS="-U_FORTIFY_SOURCE -D_FORTIFY_SOURCE=0")

    daos_build.test(unit_env, 'drpc_tests',
                    ['drpc_tests.c', '../drpc.c', '../drpc.pb-c.c',
                     common_test_utils],
                    LIBS=['protobuf-c', 'daos_common', 'gurt', 'cmocka'])
    daos_build.test(unit_env, 'acl_api_tests',
                    source=['acl_api_tests.c', '../acl_api.c'],
                    LIBS=['protobuf-c', 'daos_common', 'gurt', 'cmocka'])

if __name__ == "SCons.Script":
    scons()
