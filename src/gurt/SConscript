#!python
# (C) Copyright 2016-2021 Intel Corporation.
#
# SPDX-License-Identifier: BSD-2-Clause-Patent
#
"""Build libgurt"""

import daos_build
SRC = ['debug.c', 'dlog.c', 'hash.c', 'misc.c', 'heap.c', 'errno.c',
       'fault_inject.c', 'slab.c', 'telemetry.c']

def scons():
    """Scons function"""

    Import('env', 'prereqs', 'CART_VERSION')

    env.AppendUnique(LIBPATH=[Dir('.')])

    denv = env.Clone()

    denv.AppendUnique(CPPPATH=[Dir('.').srcnode()])
    denv.AppendUnique(LIBS=['pthread', 'yaml', 'm'])
    prereqs.require(denv, 'uuid')

    gurt_targets = denv.SharedObject(SRC)
    gurt_lib = daos_build.library(denv, 'libgurt', gurt_targets,
                                  SHLIBVERSION=CART_VERSION)
    denv.InstallVersionedLib('$PREFIX/lib64/', gurt_lib,
                             SHLIBVERSION=CART_VERSION)

    Default(gurt_lib)
    Export('gurt_lib', 'gurt_targets')

    SConscript('examples/SConscript')
    SConscript('tests/SConscript')

if __name__ == "SCons.Script":
    scons()
