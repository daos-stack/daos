"""Build DAOS components"""

import os

HEADERS = ['daos_api.h', 'daos_types.h', 'daos_errno.h', 'daos_kv.h',
           'daos_event.h', 'daos_mgmt.h', 'daos_types.h', 'daos_array.h',
           'daos_task.h', 'daos_fs.h', 'daos_uns.h', 'daos_security.h',
           'daos_prop.h', 'daos_obj_class.h', 'daos_obj.h', 'daos_pool.h',
           'daos_cont.h', 'daos_version.h', 'daos_fs_sys.h']
HEADERS_SRV = ['vos.h', 'vos_types.h']
HEADERS_GURT = ['dlog.h', 'debug.h', 'common.h', 'hash.h', 'list.h',
                'heap.h', 'fault_inject.h', 'debug_setup.h',
                'types.h', 'atomic.h', 'slab.h',
                'telemetry_consumer.h', 'telemetry_common.h']
HEADERS_CART = ['api.h', 'iv.h', 'types.h', 'swim.h']


# Keep versioned libs for now to avoid any conflict with 1.0
CART_VERSION = "4.9.0"


def generate_daos_version_header(env):
    """Generate daos_version.h from a template file"""

    tmpl_hdr_in = os.path.join('include', 'daos_version.h.in')
    sd = {'@TMPL_MAJOR@': env['API_VERSION_MAJOR'],
          '@TMPL_MINOR@': env['API_VERSION_MINOR'],
          '@TMPL_FIX@': env['API_VERSION_FIX'],
          '@TMPL_PKG_MAJOR@': env['DAOS_VERSION_MAJOR'],
          '@TMPL_PKG_MINOR@': env['DAOS_VERSION_MINOR'],
          '@TMPL_PKG_FIX@': env['DAOS_VERSION_FIX'],
          '@Template for @': ""}
    out = env.Substfile(tmpl_hdr_in, SUBST_DICT=sd)
    print("generated daos version header file: {}".format(out[0].abspath))


def check_install_header(env, header):
    """Runs a builder to ensure the header can standalone compile and installs it"""
    result = env.CheckHeader(header)
    # Ensure the target, actually gets built since none of these targets
    # get installed.
    Default(result)
    env.Install(os.path.join('$PREFIX', os.path.dirname(header)), header)


def scons():
    """Execute build"""

    Import('env', 'base_env', 'base_env_mpi', 'prereqs')

    # For Common library and headers.
    env.AppendUnique(CPPPATH=[Dir('include/daos').srcnode()])
    base_env.AppendUnique(CPPPATH=[Dir('include/daos').srcnode()])
    env.Install(os.path.join('$PREFIX', 'include/daos'), 'include/daos/tse.h')

    # Generic DAOS includes
    env.AppendUnique(CPPPATH=[Dir('include').srcnode()])
    env.AppendUnique(CPPPATH=[Dir('include')])
    base_env.AppendUnique(CPPPATH=[Dir('include').srcnode()])
    base_env.AppendUnique(CPPPATH=[Dir('include')])
    if base_env_mpi:
        base_env_mpi.AppendUnique(CPPPATH=[Dir('include').srcnode()])
        base_env_mpi.AppendUnique(CPPPATH=[Dir('include')])
    generate_daos_version_header(env)

    if not env.GetOption('clean') and not env.GetOption('help'):
        config_env = env.Clone()
        conf = Configure(config_env)
        # Detect if we have explicit_bzero
        if not conf.CheckFunc('explicit_bzero'):
            env.Append(CCFLAGS=['-DNEED_EXPLICIT_BZERO'])
            base_env.Append(CCFLAGS=['-DNEED_EXPLICIT_BZERO'])
        conf.Finish()

    for header in HEADERS:
        header_file = os.path.join('include', header)
        env.Install(os.path.join('$PREFIX', 'include'), header_file)
    if prereqs.server_requested():
        for header in HEADERS_SRV:
            header_file = os.path.join('include', 'daos_srv', header)
            env.Install(os.path.join('$PREFIX', 'include', 'daos_srv'), header_file)
    for header in HEADERS_GURT:
        check_install_header(env, os.path.join('include', 'gurt', header))
    for header in HEADERS_CART:
        check_install_header(env, os.path.join('include', 'cart', header))
    check_install_header(env, 'include/daos.h')

    env.Append(CCFLAGS=['-DCART_VERSION=\\"' + CART_VERSION + '\\"'])
    libdaos_tgts = []
    Export('env', 'CART_VERSION', 'libdaos_tgts')

    # Generate common libraries used by multiple components
    SConscript('gurt/SConscript')
    SConscript('cart/SConscript')
    SConscript('common/SConscript')
    SConscript('bio/SConscript')
    SConscript('vea/SConscript')
    SConscript('vos/SConscript')

    # Build each DAOS component
    SConscript('rsvc/SConscript')
    SConscript('mgmt/SConscript')
    SConscript('pool/SConscript')
    SConscript('container/SConscript')
    SConscript('placement/SConscript')
    SConscript('dtx/SConscript')
    SConscript('object/SConscript')
    SConscript('rebuild/SConscript')
    SConscript('security/SConscript')

    # Build DAOS client libraries
    SConscript('client/SConscript')

    # Build the common test code that requires client library
    SConscript('common/SConscript.test')

    # Build cart utilities library
    SConscript('cart/utils/SConscript')

    # rdb unit test rdbt depends on libdaos
    SConscript('rdb/SConscript')

    # Then the DAOS I/O Engine executable
    SConscript('engine/SConscript')

    # Build utilities
    SConscript('utils/SConscript')

    # Build the control plane components
    SConscript('control/SConscript')

    # Build test
    SConscript('placement/tests/SConscript')
    SConscript('tests/SConscript')

if __name__ == "SCons.Script":
    scons()
