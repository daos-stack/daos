"""Build DAOS utilities"""
import daos_build
Import('env', 'base_env', 'prereqs')

def scons():
    """Execute build"""

    if not prereqs.client_requested():
        return

    libs = ['daos', 'daos_common', 'uuid', 'dfs', 'duns', 'gurt', 'cart', 'dl']

    daos_build.add_build_rpath(env)
    daos_build.add_build_rpath(base_env)
    env.AppendUnique(LIBPATH=[Dir('.')])

    denv = env.Clone()

    # Build self test
    SConscript('self_test/SConscript')
    Default('self_test')

    if prereqs.test_requested():
        # Build crt_launch
        SConscript('crt_launch/SConscript')

        # Build daos_metrics
        SConscript('daos_metrics/SConscript')

    # Build cart_ctl
    SConscript('ctl/SConscript')

    denv.AppendUnique(LIBPATH=["../client/dfs"])

    daos_hdlrs_src = ['daos_hdlr.c', 'daos_autotest.c', 'daos_dfs_hdlr.c']
    daos_hdlrs_lib = daos_build.library(denv, 'daos_cmd_hdlrs',
                                        daos_hdlrs_src,
                                        LIBS=libs)
    env.Install('$PREFIX/lib64/', daos_hdlrs_lib)
    libs += ['daos_cmd_hdlrs']

    Export('daos_hdlrs_lib')

if __name__ == "SCons.Script":
    scons()
