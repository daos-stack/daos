# pylint: disable-next=wrong-spelling-in-comment
"""Build tests"""


def scons():
    """Execute build"""
    Import('env', 'prereqs')

    denv = env.Clone()

    # Add runtime paths for daos libraries
    denv.AppendUnique(RPATH_FULL=['$PREFIX/lib64/daos_srv'])
    denv.AppendUnique(RPATH_FULL=['$PREFIX/lib64'])

    prereqs.require(denv, 'argobots', 'protobufc', 'pmdk', 'spdk')

    # for ddb includes
    denv.AppendUnique(CPPPATH=[Dir('../').srcnode()])
    denv.AppendUnique(LIBPATH=[Dir('../')])

    # Add include directory for vos internal headers
    denv.AppendUnique(CPPPATH=[Dir('../../../vos/').srcnode()])
    denv.AppendUnique(CPPPATH=[Dir('../../../vos/')])
    denv.AppendUnique(CPPDEFINES='_GNU_SOURCE')

    libs = ['vos', 'daos_common_pmem', 'abt', 'gurt', 'uuid', 'bio', 'cart',
            'cmocka', 'ddb']
    # spdk libraries
    spdk_libs = ['spdk_event', 'spdk_log']
    spdk_libs += ['spdk_bdev', 'spdk_blob', 'spdk_blob_bdev', 'spdk_json']
    spdk_libs += ['spdk_nvme', 'spdk_init', 'spdk_thread', 'spdk_log']
    spdk_libs += ['spdk_env_dpdk', 'spdk_thread', 'spdk_bdev', 'rte_mempool']
    spdk_libs += ['rte_mempool_ring', 'rte_bus_pci', 'rte_pci', 'rte_ring']
    spdk_libs += ['rte_mbuf', 'rte_eal', 'rte_kvargs', 'spdk_bdev_aio']
    spdk_libs += ['spdk_bdev_nvme', 'spdk_blob', 'spdk_nvme', 'spdk_util']
    spdk_libs += ['spdk_json', 'spdk_jsonrpc', 'spdk_rpc', 'spdk_trace']
    spdk_libs += ['spdk_sock', 'spdk_log', 'spdk_notify', 'spdk_blob_bdev']
    spdk_libs += ['spdk_vmd', 'spdk_event_bdev', 'spdk_init', 'rte_power']
    src = ['ddb_cmd_options_tests.c',
           'ddb_commands_tests.c',
           'ddb_main_tests.c',
           'ddb_parse_tests.c',
           'ddb_path_tests.c',
           'ddb_test_driver.c',
           'ddb_vos_tests.c',
           'ddb_commands_print_tests.c']
    ddb_tests = denv.d_program('ddb_tests', [src], LIBS=libs + spdk_libs)

    denv.Install('$PREFIX/bin/', ddb_tests)

    # Build unit tests
    denv = env.Clone()
    prereqs.require(denv, 'argobots', 'spdk')
    libs = ['uuid', 'daos_common_pmem', 'gurt', 'vea', 'abt', 'bio', 'cmocka', 'pthread']
    denv.AppendUnique(RPATH_FULL=['$PREFIX/lib64/daos_srv'])
    denv.AppendUnique(CPPPATH=[Dir('../').srcnode()])
    denv.AppendUnique(CPPPATH=[Dir('../../../vos/').srcnode()])
    denv.AppendUnique(CPPPATH=[Dir('../../../bio/').srcnode()])
    # Required for dtx_act_discard_invalid tests.
    # This function is validated by its respective unit tests.
    denv.AppendUnique(LINKFLAGS=['-Wl,--wrap=vos_dtx_discard_invalid'])

    denv.Append(CPPDEFINES=['-DDAOS_PMEM_BUILD'])
    vos_src = Glob('../../../vos/*.c')

    mock_src = Glob('../../../dtx/tests/*_mock.c')
    src = ['ddb_ut.c',
           'ddb_vos_ut.c',
           '../ddb_commands.c',
           '../ddb_vos.c',
           '../ddb_main.c',
           '../ddb_printer.c',
           '../ddb_tree_path.c',
           '../ddb_parse.c',
           '../ddb.c',
           '../ddb_spdk.c',
           ]
    ddb_ut = denv.d_program('ddb_ut', src + vos_src + mock_src, LIBS=libs + spdk_libs)

    denv.Install('$PREFIX/bin/', ddb_ut)


if __name__ == "SCons.Script":
    scons()
