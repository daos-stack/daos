"""Build management server module"""
import daos_build

def scons():
    """Execute build"""
    Import('env')

    env.AppendUnique(LIBPATH=[Dir('.')])

    denv = env.Clone()

    common = denv.SharedObject(['rpc.c', 'mgmt.pb-c.c', 'pool.pb-c.c',
                                'srv.pb-c.c'])
    # Management server module
    mgmt_srv = daos_build.library(denv, 'mgmt',
                                  [common, 'srv.c', 'srv_layout.c',
                                   'srv_pool.c', 'srv_system.c',
                                   'srv_target.c'])
    denv.Install('$PREFIX/lib/daos_srv', mgmt_srv)

    # Management client library
    dc_mgmt_tgts = common + denv.SharedObject(['cli_mgmt.c', 'cli_pool.c'])
    Export('dc_mgmt_tgts')

if __name__ == "SCons.Script":
    scons()
