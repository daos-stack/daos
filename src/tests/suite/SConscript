"""Build test suite"""
import daos_build

def scons():
    """Execute build"""
    Import('denv')

    libraries = ['daos_common', 'daos', 'dfs', 'daos_tests', 'gurt', 'cart']
    libraries += ['uuid', 'dfs', 'cmocka', 'pthread']

    denv.AppendUnique(LIBPATH=["$BUILD_DIR/src/client/dfs"])

    daos_test_tgt = denv.SharedObject(['daos_test_common.c'])
    Export('daos_test_tgt')

    daos_epoch_io = denv.SharedObject(['daos_epoch_io.c'])
    Export('daos_epoch_io')

    daos_test_obj = denv.SharedObject(['daos_obj.c'])
    Export('daos_test_obj')

    test = daos_build.program(denv, 'daos_test', Glob('*.c'), LIBS=libraries)
    denv.Install('$PREFIX/bin/', test)
    denv.Install('$PREFIX/bin/io_conf', Glob('io_conf/daos_io_conf_1'))
    denv.Install('$PREFIX/bin/io_conf', Glob('io_conf/daos_io_conf_2'))
    denv.Install('$PREFIX/bin/io_conf', Glob('io_conf/daos_io_conf_3'))
    SConscript('io_conf/SConscript', exports='denv')

if __name__ == "SCons.Script":
    scons()
