# (C) Copyright 2016-2023 Intel Corporation.
#
# SPDX-License-Identifier: BSD-2-Clause-Patent
#
"""Unit tests"""

import os

TEST_SRC = ['test_linkage.cpp', 'utest_hlc.c', 'utest_swim.c',
            'utest_portnumber.c', 'utest_protocol.c']
LIBPATH = [Dir('../../'), Dir('../../../gurt')]


def scons():
    """Scons function"""
    if GetOption('help'):
        return

    Import('env', 'cart_targets', 'swim_targets', 'gurt_targets')

    test_env = env.Clone()
    test_env.require('mercury', 'uuid', 'cmocka')
    test_env.AppendUnique(LIBS=['pthread', 'm', 'dl'])
    test_env.AppendUnique(CXXFLAGS=['-std=c++0x'])
    test_env.AppendUnique(LIBPATH=LIBPATH)
    test_env.AppendUnique(RPATH_FULL=LIBPATH)

    for test in TEST_SRC:
        testobj = test_env.Object(test)
        testname = os.path.splitext(test)[0]
        testprog = test_env.d_test_program(target=testname,
                                           source=[testobj, cart_targets, swim_targets,
                                                   gurt_targets],
                                           LIBS=test_env["LIBS"] + ['yaml'])
        Default(testprog)


if __name__ == "SCons.Script":
    scons()
